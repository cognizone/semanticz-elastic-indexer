plugins { 
    id 'java'
    id 'io.freefair.lombok' version '8.10'
    id 'org.owasp.dependencycheck' version '10.0.3'
    id 'maven-publish'
    id 'signing'
    id 'pl.allegro.tech.build.axion-release' version '1.13.3'
}

group = 'zone.cogni.semanticz'
version = scmVersion.version

repositories {
    mavenLocal()
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    withJavadocJar()
    withSourcesJar()
}

scmVersion {
    tag {
        prefix = "v"
        versionSeparator = ""
        branchPrefix = [
                'release/.*': "release-v",
                'hotfix/.*': "hotfix-v"
        ]
    }
    nextVersion {
        suffix = "SNAPSHOT"
        separator = "-"
    }
    versionIncrementer("incrementPatch")
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation "org.apache.jena:apache-jena-libs:4.10.0"
    implementation "jakarta.json:jakarta.json-api:2.0.1"
    implementation "co.elastic.clients:elasticsearch-java:7.17.4"
    implementation "io.vavr:vavr:0.9.2"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)

            pom {
                name.set("elastic-indexer")
                description.set("The Elastic Indexer is a Java-based tool that helps in indexing and searching documents in an Elasticsearch cluster using simple and bulk methods.")
                url.set("https://github.com/cognizone/elastic-indexer")

                scm {
                    connection.set("scm:git@github.com:cognizone/elastic-indexer.git")
                    developerConnection.set("scm:git@github.com:cognizone/semanticz-elastic-indexer.git")
                    url.set("https://github.com/cognizone/semanticz-elastic-indexer.git")
                }

                licenses {
                    license {
                        name.set("The Apache License, Version 2.0")
                        url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
                    }
                }

                developers {
                    developer {
                        id.set("cognizone")
                        name.set("Cognizone")
                        email.set("dev@cognizone.com")
                    }
                }
            }
        }
    }
}

tasks.register("qualityCheck") {
    dependsOn(tasks.named("pmdMain"))
    dependsOn(tasks.named("pmdTest"))
    dependsOn(tasks.named("dependencyCheckAnalyze"))
}

tasks.test {
    useJUnitPlatform()
}

tasks.jar {
    from("${projectDir}") {
        include("LICENSE")
        into("META-INF")
    }
}
